define(
  hsv, { h: Int; s: Int; v: Int }, Int,
  {
    ret = 0
    if(
      [$s == 0, ret = { { $v * 65536 } + { $v * 256 } + $v }],
      [true,
        {
          region = { $h / 256 }
          remainder = { $h % 256 }
          p = { {$v * {255 - $s}} / 255 }
          q = { {$v * {255 - {{$s * $remainder} / 256}}} / 255 }
          t = { {$v * {255 - {{$s * {255 - $remainder}} / 256}}} / 255 }
          r = 0
          g = 0
          b = 0
          if(
            [$region == 0, {r = $v; g = $t; b = $p}],
            [$region == 1, {r = $q; g = $v; b = $p}],
            [$region == 2, {r = $p; g = $v; b = $t}],
            [$region == 3, {r = $p; g = $q; b = $v}],
            [$region == 4, {r = $t; g = $p; b = $v}],
            [true,         {r = $v; g = $p; b = $q}]
          )
          ret = { { $r * 256 * 256 } + { $g * 256 } + $b }
        }
      ]
    )
    $ret
  }
)
include("mandelbrot.jspl", mandelbrot)
define(
  main, {x: Int; y: Int; frame: Int; mx: Int; my: Int }, Int,
  {
    width = 512.0
    height = 512.0
    zoom = 0.0625
    type = 0
    if([$x >= 0, type = 1])
    if([$y >= 0, type = {$type + 2}])
    center_x = 0.0
    center_y = 0.0
    if(
      [$type == 0, { center_x = -0.7435; center_y = 0.1855 }],
      [$type == 1, { center_y = -0.745 }],
      [$type == 2, { center_x = -1.5  }],
      [$type == 3, { center_x = 0.2537361   }]
    )
    tmp_frame = {$frame % 128}
    if([$tmp_frame >= 120, tmp_frame = 120])
    while($tmp_frame > 7, { zoom = {$zoom / 1.125}; tmp_frame = {$tmp_frame - 1} })
    scale_x = $zoom
    scale_y = { $zoom * { $height / $width } }
    fx = {Float($x) + {$width / 4.0}}
    fy = {Float($y) + {$height / 4.0}}
    if(
      [$type == 1, { fx = {$fx - {$width / 2.0}} }],
      [$type == 2, { fy = {$fy - {$height / 2.0}} }],
      [$type == 3, {
        fx = {$fx - {$width / 2.0}}
        fy = {$fy - {$height / 2.0}}
      }]
    )
    fx = {$fx * $scale_x}
    fy = {$fy * $scale_y}
    fx = {$center_x + $fx}
    fy = {$center_y + $fy}
    iter = mandelbrot($fx, $fy)
    h = {{{$iter * 1535} / 50} + {{$iter % 2} * 728}}
    s = 255
    v = 0
    if([$iter != 50, v = 192])
    hsv($h, $s, $v)
  }
)
GUI(main)
