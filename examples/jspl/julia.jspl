define(
  julia, { zx: Float; zy: Float }, Int,
  {
    cr = -0.7
    ci = 0.27015
    i = 0
    while(
      { $i < 50 } and { Int( { $zx * $zx } + { $zy * $zy } ) < 4 },
      {
        tmp = { { { $zx * $zx } - { $zy * $zy } } + $cr }
        zy  = { { 2.0 * $zx * $zy } + $ci }
        zx  = $tmp
        i   = { $i + 1 }
      }
    )
    $i
  }
)

define(
  render_row, { y: Int }, Null,
  {
    x = -50
    while(
      $x < 50,
      {
        fx = { Float($x) * 0.03 }
        fy = { Float($y) * 0.06 }
        iter = julia($fx, $fy)
        str = " "
        if(
        [$iter == 50, str = "#"],
        [45 < $iter,  str = "@"],
        [40 < $iter,  str = "%"],
        [35 < $iter,  str = "&"],
        [30 < $iter,  str = "$"],
        [25 < $iter,  str = "*"],
        [20 < $iter,  str = "+"],
        [15 < $iter,  str = "="],
        [10 < $iter,  str = "-"],
        [5  < $iter,  str = "."]
        )
        print($str)
        x = { $x + 1 }
      }
    )
    print("\n")
  }
)

define(
  main, {}, Null,
  {
    y = -20
    while(
      $y < 20,
      {
        render_row($y)
        y = { $y + 1 }
      }
    )
  }
)

main()
